#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_shader_explicit_arithmetic_types_float16 : require

#include "tiled_shading.glsl"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(std430, set = 0, binding = 0) buffer writeonly frustumBuffer {
    LightVisibility visibleLightIndices[];
};

layout(push_constant) uniform PushConstants {
    mat4 inverseVp;
    ivec2 viewportSize;
} pushConstants;

f16vec3 ScreenSpaceToViewSpace(f16vec2 screenSpace) {
    f16vec2 ndc = screenSpace / f16vec2(pushConstants.viewportSize);
//    vec4 clipSpace = vec4(2.0 * vec2(ndc.x, 1.0f - ndc.y) - 1.0, -1.0, 1.0);
    f16vec4 clipSpace = f16vec4(2.0 * ndc - 1.0, -1.0, 1.0);
    f16vec4 view = f16vec4(pushConstants.inverseVp) * clipSpace;
    view /= view.w;

    return view.xyz;
}

f16vec3 LineIntersectionWithZPlane(f16vec3 lineStart, f16vec3 lineEnd, float16_t z) {
    float16_t t = (z - lineStart.z) / (lineEnd.z - lineStart.z);
    return lineStart + (lineEnd - lineStart) * t;
}

void main() {
    uint tileIndex = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x + gl_GlobalInvocationID.z * gl_NumWorkGroups.x * gl_NumWorkGroups.y;
    f16vec2 tileSize = f16vec2(pushConstants.viewportSize) / f16vec2(gl_NumWorkGroups.xy);

    f16vec2 workGroup = f16vec2(gl_WorkGroupID.xy);

    f16vec3 minTile = ScreenSpaceToViewSpace(workGroup * tileSize);
    f16vec3 maxTile = ScreenSpaceToViewSpace(workGroup * tileSize + tileSize);

    f16vec2 workGroupRatio = f16vec2(gl_WorkGroupID.z / gl_NumWorkGroups.z, (gl_WorkGroupID.z + 1) / gl_NumWorkGroups.z);
    f16vec2 planes = f16vec2(pow(10000.0hf, workGroupRatio.x), pow(10000.0hf, workGroupRatio.y)) * 0.1hf;

    f16vec3 eye = f16vec3(0);
    f16vec3 minPointNear = LineIntersectionWithZPlane(eye, minTile, planes.x);
    f16vec3 maxPointNear = LineIntersectionWithZPlane(eye, maxTile, planes.x);
    f16vec3 minPointFar = LineIntersectionWithZPlane(eye, minTile, planes.y);
    f16vec3 maxPointFar = LineIntersectionWithZPlane(eye, maxTile, planes.y);

    visibleLightIndices[tileIndex].minPoint = f16vec4(min(minPointNear, minPointFar), 0.0);
    visibleLightIndices[tileIndex].maxPoint = f16vec4(max(maxPointNear, maxPointFar), 0.0);
}